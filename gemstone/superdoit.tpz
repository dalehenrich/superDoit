#
# Note that the display and set options for controlling frame data format is controlled
# by the platform-specific gemstone/superdoit_boot_1*.tpz files
#
display oops
set singlecolumn on
login
run
"create the transient symbol dictionary for superDoit"
	| sess symbolList name |
	sess := GsCurrentSession currentSession.
	sess _transientSymbolList ifNil: [ System refreshTransientSymbolList ].
	symbolList := sess transientSymbolList.
	name := #'SuperDoit_transientSymbolDictionary'.	"must match value in SuperDoitCommandParser class >> transientSymbolDictionaryName"
	(symbolList resolveSymbol: name)
		ifNil: [
			| systemDictionary |
			"add to transient symbol dictionary if not present" 
			systemDictionary := SymbolDictionary new name: name.
			symbolList insertObject: systemDictionary at: 1 ]
%
run
(GsSession currentSession objectNamed: #Rowan)
	ifNil: [
		"Rowan not present ... bootstrap the  superDoit support code in image"
		| gemstoneVersion |
		gemstoneVersion := System gemEnvironmentVariable: 'SUPERDOIT_GEMSTONE_VERSION'.
		(#( '3.6.4' '3.6.5' '3.6.6' '3.6.7' '3.7.0' '3.7.1' ) includes: gemstoneVersion)
			ifTrue: [ 
				| bootstrapSuperDoit_file sess symbolList name |
				bootstrapSuperDoit_file := (gemstoneVersion beginsWith: '3.6')
					ifTrue: [ '$gemstoneDir/bootstrap/bootstrapSuperDoit_361x.gs']
					ifFalse: [ 
						"3.7.x"
						'$gemstoneDir/bootstrap/bootstrapSuperDoit_37x.gs' ].
				GsFileIn fromServerPath: bootstrapSuperDoit_file ]
			ifFalse: [ self error: 'Unsupported GemStone version (', gemstoneVersion, ') for using a non-Rowan extent for running .solo scripts' ] ]
	ifNotNil: [:rowanClass |
		| resolvedProject superDoitTransientSymbolDictionaryName |
		"install superDoit support code in image"
		resolvedProject := rowanClass 
			projectFromUrl: 'file:$SUPERDOIT_DIR/rowan/specs/SuperDoit_solo.ston'
			diskUrl: 'file:$SUPERDOIT_DIR'.
		superDoitTransientSymbolDictionaryName := 'SuperDoit_transientSymbolDictionary'.	"must match value in SuperDoitCommandParser class >> transientSymbolDictionaryName"
		((rowanClass respondsTo: #version) and: [ rowanClass version >= '3.0.0' asRwSemanticVersionNumber ])
			ifTrue: [ resolvedProject gemstoneSetDefaultSymbolDictNameTo: superDoitTransientSymbolDictionaryName ]
			ifFalse: [ resolvedProject _loadSpecification gemstoneSetDefaultSymbolDictNameTo: superDoitTransientSymbolDictionaryName ].
		resolvedProject load ].
%
run
false ifTrue: [
	"Set breakpoints in SuperDoitCommandParser methods. #processInputFile is a 
		good place to break to step through the whole cycle: parse and execute script
		file ... after the SuperDoitExecutionClass has been created and populated, 
		you can set breakpoints in your script methods ... if needed (self halt may 
		be better option)" 
	(GsCurrentSession currentSession symbolList objectNamed: #SuperDoitCommandParser) class
		_setMethodBreak: #processInputFile stepPoint: 30 env: 0;
		yourself.
].
false ifTrue: [
	"Set breakpoints in SuperDoitDoitCommand methods, ths is the method that ends up lauching the doit" 
	(GsCurrentSession currentSession symbolList objectNamed: #SuperDoitDoitCommand)
		_setMethodBreak: #executeAgainst: stepPoint: 24 env: 0;
		yourself.
].
false ifTrue: [
	"Set breakpoints in SuperDoitExecution methods ... better option that adding
		halts to superDoit source, unless -D is failiing you." 
		(GsCurrentSession currentSession symbolList objectNamed: #SuperDoitExecution)
		_setMethodBreak: #exit:withStatus: stepPoint: 2 env: 0;
		_setMethodBreak: #exitWithStatus: stepPoint: 2 env: 0;
		_setMethodBreak: #doit stepPoint: 1 env: 0;
		_setMethodBreak: #doit stepPoint: 18 env: 0;
		_setMethodBreak: #doit stepPoint: 20 env: 0;
		yourself.
]
%
run
	"the first non-topaz argument is the name of the script file
		extract the file name and parse the script to extract information
		needed to create class for running the script.
		The printString of the doit result is printed to stdout.
	"
	SuperDoitCommandParser processInputFile
%
